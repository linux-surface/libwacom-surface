# Maintainer: Maximilian Luz <luzmaximilian@gmail.com>

pkgname=(
    libwacom2-surface
    libwacom2-dev-surface
    libwacom9-surface
    libwacom9-dev-surface
    libwacom-common-surface
    libwacom-bin-surface
    libwacom-surface
)
_pkgver1=1.12.1
_pkgver2=2.13.0
pkgver=2.13.0
pkgrel=1
pkgdesc="Patched libwacom for Microsoft Surface devices"
arch=('amd64')
url="https://github.com/linux-surface/libwacom"
license=('MIT')
depends=()
makedepends=(
    'meson'
    'libglib2.0-dev'
    'libgudev-1.0-dev'
    'libxml2-dev'
    'libevdev-dev'
    'udev'
)
checkdepends=(
    'python3-libevdev'
    'python3-pytest'
    'python3-pyudev'
)
validpgpkeys=(
    '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF'
    '594ABBA066118C7A02D10A80A8AF906D9307FBAD'
)

source=(
    "https://github.com/linuxwacom/libwacom/releases/download/libwacom-${_pkgver1}/libwacom-${_pkgver1}.tar.bz2"{,.sig}
    "https://github.com/linuxwacom/libwacom/releases/download/libwacom-${_pkgver2}/libwacom-${_pkgver2}.tar.xz"{,.sig}
    '0001-Revert-minimum-meson-version-to-v0.53.0.patch'
    'v1-0001-Add-support-for-BUS_VIRTUAL.patch'
    'v1-0002-Add-support-for-Intel-Management-Engine-bus.patch'
    'v1-0003-data-Add-Microsoft-Surface-Pro-3.patch'
    'v1-0004-data-Add-Microsoft-Surface-Pro-4.patch'
    'v1-0005-data-Add-Microsoft-Surface-Pro-5.patch'
    'v1-0006-data-Add-Microsoft-Surface-Pro-6.patch'
    'v1-0007-data-Add-Microsoft-Surface-Pro-7.patch'
    'v1-0008-data-Add-Microsoft-Surface-Pro-7.patch'
    'v1-0009-data-Add-Microsoft-Surface-Pro-8.patch'
    'v1-0010-data-Add-Microsoft-Surface-Pro-9.patch'
    'v1-0011-data-Add-Microsoft-Surface-Book.patch'
    'v1-0012-data-Add-Microsoft-Surface-Book-2-13.5.patch'
    'v1-0013-data-Add-Microsoft-Surface-Book-2-15.patch'
    'v1-0014-data-Add-Microsoft-Surface-Book-3-13.5.patch'
    'v1-0015-data-Add-Microsoft-Surface-Book-3-15.patch'
    'v1-0016-data-Add-Microsoft-Surface-Laptop-Studio.patch'
    'v2-0001-Add-support-for-BUS_VIRTUAL.patch'
    'v2-0002-Add-support-for-Intel-Management-Engine-bus.patch'
    'v2-0003-data-Add-Microsoft-Surface-Pro-3.patch'
    'v2-0004-data-Add-Microsoft-Surface-Pro-4.patch'
    'v2-0005-data-Add-Microsoft-Surface-Pro-5.patch'
    'v2-0006-data-Add-Microsoft-Surface-Pro-6.patch'
    'v2-0007-data-Add-Microsoft-Surface-Pro-7.patch'
    'v2-0008-data-Add-Microsoft-Surface-Pro-7.patch'
    'v2-0009-data-Add-Microsoft-Surface-Pro-8.patch'
    'v2-0010-data-Add-Microsoft-Surface-Pro-9.patch'
    'v2-0011-data-Add-Microsoft-Surface-Book.patch'
    'v2-0012-data-Add-Microsoft-Surface-Book-2-13.5.patch'
    'v2-0013-data-Add-Microsoft-Surface-Book-2-15.patch'
    'v2-0014-data-Add-Microsoft-Surface-Book-3-13.5.patch'
    'v2-0015-data-Add-Microsoft-Surface-Book-3-15.patch'
    'v2-0016-data-Add-Microsoft-Surface-Laptop-Studio.patch'
)
sha256sums=('053687f64fdb1c8947cf0e54743c47bfcfba8ef3cb22466a1faa1e2c37ccb315'
            'SKIP'
            'acd18121441bbc00fc5c881fca08a33319ab814b798eac8d0be6354923f8fb08'
            'SKIP'
            '53b91d9e85d3542181873940cc9cf72ec042ae575cc23904fb49f4ee8a63933c'
            'c504893656f851832116616e6e5bd50b3d0ac36f7288042dc7eb50e396212bc4'
            'c64275d8129ed21c58442cda159c90bea6e97813ee91e3911344dd0760a6f35c'
            '279ad2b83cc9d7ee855ee24b5fe952472483b8d4d2f8884b10a9cc12fac7256d'
            '2acccdbe30ed5a672dc1f85112a3cb2e86be1108e19e46470af328d7169602db'
            'f96e62ff26b53dd185cd2b73e42cd49771f9f54ef47e93902a77718264999d84'
            '4f24bb4ea3bf6b2a6cb543727d61d9939b43e5ecb3e693f5fc437508dafbfa05'
            'c95b22ce7e5fa87c641255aef705b3678eba63807e098bfa71bd22b36dc6fdb5'
            '5171bba9e236f7643b516ce820828bca28ab6c49d556d6097d3143d3956aae9e'
            '29216be1a22bb8b178d2cde6f8f52e9a956deffd1c4944de396ac032d46be76e'
            '277b95ed2a7569242f4d6d4943ec01e76deb2ba2b192df99955969f486985ffc'
            '200c17a8cd6891f2f8703dd99ab029e58ea7ebfd21cfdcb3d04cd0d6fdfb28eb'
            '153f01644b46e43945a0fde01278a10a7dd0bc0091f638d65a055b8c80b56040'
            '0f9f533afe2842003b676052a89ca38e894045291fae0dc74f9221f4e35af077'
            '3113ca84ad91f05a5c7f5076e3802ed6c8cd3805ea6bb8cd60c44d0cfe234ced'
            '554b6f6bd1f283094daf8911dfc288b4f88f3ae9e63cd450bb24fef89fc07d5a'
            'e780cf1f85edb37a675109574873d671ccc4e9bbac191e8f9685a4a1a79ced0c'
            '696164b851014a204cb42dcee0b04e98680df4154494454aed499e1928811668'
            'd7ba3b1fa5d29d3905be5bc6b70bb351afebd1343b57ca8b213af1279b7ded49'
            'cf8051976e8c2a52484cb66e9aba57dffef234931d879bd33704b9012656b517'
            'f8b1dbff6cd13e7da6bfa5dc6b009f19588c8fccaff1d262960b0345604ecc9a'
            '13bbaafb309cd900e8d9292955075521390849933fd6d8481ea34ac9b992f909'
            '09d92bec44dadf9655d881dbb0c71edd32c5a897c40bd9edf13314c5c8ac46d2'
            '29a7da851ea01ea1ff5403318a40c4da5f8ab842cbffa221891a2d68f17d5ee7'
            '6360efe22f06676396adccdf48ed66349aa47a595bebcfab4a8ff04eee8537bf'
            '93b04852ad3e1441e1c49541227c188d1dbf23ebeab94ba26d36e29818b0cf59'
            '46cf653beb245639cc7044a5c03a1a654d5072d8ab6d9d1aebe9810fdc2f2393'
            'a32dcf496234bbb960f519cfa49127bdb15de9d3aa965c40e4ec9ea561d55eb1'
            '0b14ea69e7038169d309618a5767bfc2de2b5895e1bcc8ec4c4b606466835524'
            '5787ed06c0c0c5b099666927b232fc4299bd9589c3971f3affb00b982356753a'
            'df9a4d9cb980dff8644e8e5070e396079397fac4a941cdbf5417742842f9c7f1'
            '6baf29f63a1d7366ee6b9a8ca11359eb57cac9fcaf276caa3e7ac9e29fcbefce'
            '77afbd2151461fb0a4e3353a3c7e3af7ec37dbdaf0edd519d69e1894ccecaa0d')

prepare() {
    msg2 "patching libwacom-${_pkgver1}"
    cd "${srcdir}/libwacom-${_pkgver1}"
    patch -Np1 -i "${srcdir}/0001-Revert-minimum-meson-version-to-v0.53.0.patch" || true
    for p in "${srcdir}/v1-"*.patch ; do
        patch -Np1 -i "${p}" || true
    done

    msg2 "patching libwacom-${_pkgver2}"
    cd "${srcdir}/libwacom-${_pkgver2}"
    patch -Np1 -i "${srcdir}/0001-Revert-minimum-meson-version-to-v0.53.0.patch" || true
    for p in "${srcdir}/v2-"*.patch ; do
        patch -Np1 -i "${p}" || true
    done
}

build() {
    msg2 "build libwacom ${_pkgver1}"
    cd "${srcdir}/libwacom-${_pkgver1}"
    meson build . --prefix="/usr"
    ninja -C build

    msg2 "build libwacom ${_pkgver2}"
    cd "${srcdir}/libwacom-${_pkgver2}"
    meson build . --prefix="/usr"
    ninja -C build
}

package_libwacom2-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom2'
    )
    provides=(
        "libwacom2=${_pkgver1}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver1}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/pkgconfig"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so"
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom2-dev-surface() {
    depends=(
        'libglib2.0-dev'
        'libgudev-1.0-dev'
        "libwacom2-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-dev'
        'libwacom9-dev-surface'
    )
    provides=(
        "libwacom-dev=${_pkgver1}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver1}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so."*
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom9-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom9'
    )
    provides=(
        "libwacom9=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/pkgconfig"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so"
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom9-dev-surface() {
    depends=(
        'libglib2.0-dev'
        'libgudev-1.0-dev'
        "libwacom9-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-dev'
        'libwacom2-dev-surface'
    )
    provides=(
        "libwacom-dev=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so."*
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-bin-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom9-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-bin'
    )
    provides=(
        "libwacom-bin=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib"
    rm -rf "${pkgdir}/usr/share/libwacom"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-common-surface() {
    conflicts=(
        'libwacom-common'
    )
    provides=(
        "libwacom-common=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu"
    rm -rf "${pkgdir}/usr/share/man"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-surface() {
    depends=(
        "libwacom2-surface=${pkgver}-${pkgrel}"
        "libwacom9-surface=${pkgver}-${pkgrel}"
        "libwacom-bin-surface=${pkgver}-${pkgrel}"
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )

    true
}
