# Maintainer: Maximilian Luz <luzmaximilian@gmail.com>

pkgname=(
    libwacom2-surface
    libwacom2-dev-surface
    libwacom9-surface
    libwacom9-dev-surface
    libwacom-common-surface
    libwacom-bin-surface
    libwacom-surface
)
_pkgver1=1.12.1
_pkgver2=2.12.2
pkgver=2.12.2
pkgrel=1
pkgdesc="Patched libwacom for Microsoft Surface devices"
arch=('amd64')
url="https://github.com/linux-surface/libwacom"
license=('MIT')
depends=()
makedepends=(
    'meson'
    'libglib2.0-dev'
    'libgudev-1.0-dev'
    'libxml2-dev'
    'libevdev-dev'
    'udev'
)
checkdepends=(
    'python3-libevdev'
    'python3-pytest'
    'python3-pyudev'
)
validpgpkeys=(
    '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF'
    '594ABBA066118C7A02D10A80A8AF906D9307FBAD'
)

source=(
    "https://github.com/linuxwacom/libwacom/releases/download/libwacom-${_pkgver1}/libwacom-${_pkgver1}.tar.bz2"{,.sig}
    "https://github.com/linuxwacom/libwacom/releases/download/libwacom-${_pkgver2}/libwacom-${_pkgver2}.tar.xz"{,.sig}
    '0001-Revert-minimum-meson-version-to-v0.53.0.patch'
    'v1-0001-Add-support-for-BUS_VIRTUAL.patch'
    'v1-0002-Add-support-for-Intel-Management-Engine-bus.patch'
    'v1-0003-data-Add-Microsoft-Surface-Pro-3.patch'
    'v1-0004-data-Add-Microsoft-Surface-Pro-4.patch'
    'v1-0005-data-Add-Microsoft-Surface-Pro-5.patch'
    'v1-0006-data-Add-Microsoft-Surface-Pro-6.patch'
    'v1-0007-data-Add-Microsoft-Surface-Pro-7.patch'
    'v1-0008-data-Add-Microsoft-Surface-Pro-7.patch'
    'v1-0009-data-Add-Microsoft-Surface-Pro-8.patch'
    'v1-0010-data-Add-Microsoft-Surface-Pro-9.patch'
    'v1-0011-data-Add-Microsoft-Surface-Book.patch'
    'v1-0012-data-Add-Microsoft-Surface-Book-2-13.5.patch'
    'v1-0013-data-Add-Microsoft-Surface-Book-2-15.patch'
    'v1-0014-data-Add-Microsoft-Surface-Book-3-13.5.patch'
    'v1-0015-data-Add-Microsoft-Surface-Book-3-15.patch'
    'v1-0016-data-Add-Microsoft-Surface-Laptop-Studio.patch'
    'v2-0001-Add-support-for-BUS_VIRTUAL.patch'
    'v2-0002-Add-support-for-Intel-Management-Engine-bus.patch'
    'v2-0003-data-Add-Microsoft-Surface-Pro-3.patch'
    'v2-0004-data-Add-Microsoft-Surface-Pro-4.patch'
    'v2-0005-data-Add-Microsoft-Surface-Pro-5.patch'
    'v2-0006-data-Add-Microsoft-Surface-Pro-6.patch'
    'v2-0007-data-Add-Microsoft-Surface-Pro-7.patch'
    'v2-0008-data-Add-Microsoft-Surface-Pro-7.patch'
    'v2-0009-data-Add-Microsoft-Surface-Pro-8.patch'
    'v2-0010-data-Add-Microsoft-Surface-Pro-9.patch'
    'v2-0011-data-Add-Microsoft-Surface-Book.patch'
    'v2-0012-data-Add-Microsoft-Surface-Book-2-13.5.patch'
    'v2-0013-data-Add-Microsoft-Surface-Book-2-15.patch'
    'v2-0014-data-Add-Microsoft-Surface-Book-3-13.5.patch'
    'v2-0015-data-Add-Microsoft-Surface-Book-3-15.patch'
    'v2-0016-data-Add-Microsoft-Surface-Laptop-Studio.patch'
)
sha256sums=('053687f64fdb1c8947cf0e54743c47bfcfba8ef3cb22466a1faa1e2c37ccb315'
            'SKIP'
            'c8319c40c70edd05d1839c0d3f449c23bdc90cd1f0d819bd0c1ec7c00b117700'
            'SKIP'
            'c6bf372cf0ff9001add9dea672a5c23702bbb697d98dba83a034e52fc2805979'
            'c504893656f851832116616e6e5bd50b3d0ac36f7288042dc7eb50e396212bc4'
            'c64275d8129ed21c58442cda159c90bea6e97813ee91e3911344dd0760a6f35c'
            '279ad2b83cc9d7ee855ee24b5fe952472483b8d4d2f8884b10a9cc12fac7256d'
            '2acccdbe30ed5a672dc1f85112a3cb2e86be1108e19e46470af328d7169602db'
            'f96e62ff26b53dd185cd2b73e42cd49771f9f54ef47e93902a77718264999d84'
            '4f24bb4ea3bf6b2a6cb543727d61d9939b43e5ecb3e693f5fc437508dafbfa05'
            'c95b22ce7e5fa87c641255aef705b3678eba63807e098bfa71bd22b36dc6fdb5'
            '5171bba9e236f7643b516ce820828bca28ab6c49d556d6097d3143d3956aae9e'
            '29216be1a22bb8b178d2cde6f8f52e9a956deffd1c4944de396ac032d46be76e'
            '277b95ed2a7569242f4d6d4943ec01e76deb2ba2b192df99955969f486985ffc'
            '200c17a8cd6891f2f8703dd99ab029e58ea7ebfd21cfdcb3d04cd0d6fdfb28eb'
            '153f01644b46e43945a0fde01278a10a7dd0bc0091f638d65a055b8c80b56040'
            '0f9f533afe2842003b676052a89ca38e894045291fae0dc74f9221f4e35af077'
            '3113ca84ad91f05a5c7f5076e3802ed6c8cd3805ea6bb8cd60c44d0cfe234ced'
            '554b6f6bd1f283094daf8911dfc288b4f88f3ae9e63cd450bb24fef89fc07d5a'
            'e780cf1f85edb37a675109574873d671ccc4e9bbac191e8f9685a4a1a79ced0c'
            '7c04ca0e57093f1e942ef41490f0b029fb0286914e1559b76165d0b14daf89b5'
            '6be7da91006cd120b4fed933a1b17c27a6fcb1ce04381775b7060b1a0c39d885'
            'bfbc0fdd5985a731a2c1a8fdea78d221bd4173c5c76b1a94c338c609845ee2eb'
            '42f94cb4312842e33233b8e1195be00d371a7b070cdccaa29220bba488bd3a4d'
            '1ed950b8968780ffd30245ad3481b7213e36f632ebffba26049419236ace2160'
            'a50e1c38db3e18ca28d4b62788a57e04806bc5f5c1dbacb27a68e12add83cba3'
            'd3bd6325a80439c645ce664460c76867094f9652521705501a9d852995b41979'
            '01ff12b4a4f569feea0f869548d7cc2f6a295525157e0188468189130c6e4e74'
            '80ffaa494ab055e4863578c462dde74029235e39a2bae58c282d59fe136402d6'
            'd553efe7a31d34f67c52a3748993d8e07ab236686207a0163ca4bbee6ab571bc'
            '35b31550b133ec8f607635b8898eab1d31aa13b03b2af34e8003eb71ab1d32ef'
            'e48269db908628d5f153094a08c7cf3e04bff5e5aa674ea071020389e4be100e'
            '77aa08c2e1401add8387a1248ea23c85c8fa40a7a96a95820eefe53aaf154351'
            'c8b723002a7b33bf9e2ba93a73fa35cdd101ae100f4519d36b910379a58e16fb'
            '3eea2d2d28d1fe3eea4b5f578f912e5d9dbd15a0e7c2f30b6c9283e30b00ff30'
            'fffb14de0426a9a8ecdbb83f5531399dbcb16b66d283a7e3098d27d344216499')

prepare() {
    msg2 "patching libwacom-${_pkgver1}"
    cd "${srcdir}/libwacom-${_pkgver1}"
    patch -Np1 -i "${srcdir}/0001-Revert-minimum-meson-version-to-v0.53.0.patch" || true
    for p in "${srcdir}/v1-"*.patch ; do
        patch -Np1 -i "${p}" || true
    done

    msg2 "patching libwacom-${_pkgver2}"
    cd "${srcdir}/libwacom-${_pkgver2}"
    patch -Np1 -i "${srcdir}/0001-Revert-minimum-meson-version-to-v0.53.0.patch" || true
    for p in "${srcdir}/v2-"*.patch ; do
        patch -Np1 -i "${p}" || true
    done
}

build() {
    msg2 "build libwacom ${_pkgver1}"
    cd "${srcdir}/libwacom-${_pkgver1}"
    meson build . --prefix="/usr"
    ninja -C build

    msg2 "build libwacom ${_pkgver2}"
    cd "${srcdir}/libwacom-${_pkgver2}"
    meson build . --prefix="/usr"
    ninja -C build
}

package_libwacom2-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom2'
    )
    provides=(
        "libwacom2=${_pkgver1}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver1}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/pkgconfig"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so"
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom2-dev-surface() {
    depends=(
        'libglib2.0-dev'
        'libgudev-1.0-dev'
        "libwacom2-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-dev'
        'libwacom9-dev-surface'
    )
    provides=(
        "libwacom-dev=${_pkgver1}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver1}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so."*
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom9-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom9'
    )
    provides=(
        "libwacom9=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/pkgconfig"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so"
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom9-dev-surface() {
    depends=(
        'libglib2.0-dev'
        'libgudev-1.0-dev'
        "libwacom9-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-dev'
        'libwacom2-dev-surface'
    )
    provides=(
        "libwacom-dev=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/lib/udev"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu/libwacom.so."*
    rm -rf "${pkgdir}/usr/share"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-bin-surface() {
    depends=(
        'libglib2.0-0'
        'libgudev-1.0-0'
        'systemd'
        "libwacom9-surface=${pkgver}-${pkgrel}"
    )
    conflicts=(
        'libwacom-bin'
    )
    provides=(
        "libwacom-bin=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib"
    rm -rf "${pkgdir}/usr/share/libwacom"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-common-surface() {
    conflicts=(
        'libwacom-common'
    )
    provides=(
        "libwacom-common=${_pkgver2}-${pkgrel}"
    )

    cd "${srcdir}/libwacom-${_pkgver2}"

    # install everything
    DESTDIR="${pkgdir}" ninja install -C build

    # remove stuff that doesn't go in this package
    rm -rf "${pkgdir}/usr/bin"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/lib/x86_64-linux-gnu"
    rm -rf "${pkgdir}/usr/share/man"

    # copy license
    install -D -m644 "COPYING" "${pkgdir}/usr/share/doc/${pkgname}/copyright"
}

package_libwacom-surface() {
    depends=(
        "libwacom2-surface=${pkgver}-${pkgrel}"
        "libwacom9-surface=${pkgver}-${pkgrel}"
        "libwacom-bin-surface=${pkgver}-${pkgrel}"
        "libwacom-common-surface=${pkgver}-${pkgrel}"
    )

    true
}
